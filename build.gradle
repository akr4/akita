buildscript {
  repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:1.0.0.RC4"
    classpath "com.github.rodionmoiseev.gradle.plugins:idea-utils:0.2"
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'idea-utils'
apply plugin: 'spring-boot'

jar {
  baseName = 'akita'
  version =  '0.1.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
  logbackVersion = '1.0.9'
}

configurations {
  all*.exclude group: "commons-logging", module: "commons-logging"
  provided
}

repositories {
  mavenCentral()
  maven { url "http://repo.spring.io/libs-snapshot" }
}

dependencies {
  compile "postgresql:postgresql:9.1-901.jdbc4"

  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: "spring-boot-starter-tomcat"
  }
  compile "org.springframework.boot:spring-boot-starter-jetty"
  compile "org.springframework.boot:spring-boot-starter-jdbc"
  compile "org.springframework.boot:spring-boot-starter-actuator"

  compile "org.projectlombok:lombok:1.12.6"

  compile "org.slf4j:jcl-over-slf4j:1.7.2"
  compile "ch.qos.logback:logback-classic:$logbackVersion"
  runtime "org.codehaus.groovy:groovy:1.8.6"

  compile "commons-dbcp:commons-dbcp:1.4"
  compile "com.googlecode.flyway:flyway-core:2.2.1"

  testCompile "junit:junit"
  testCompile "org.mockito:mockito-core:1.9.5"
}

idea {
  project {
    // customize the .ipr file generation.
    ipr {
      withXml { xmlProvider ->
        def project = xmlProvider.asNode()

        // Add appropriate groovy files to the compiler resources. These files will not be compiled by the groovy compiler but will be copied to the classes directory during compilation.
        def compilerSettings = project.component.find { it.@name == 'CompilerConfiguration' }
        compilerSettings.wildcardResourcePatterns*.appendNode('entry', [name: '*/logback*.groovy'])
       compilerSettings.annotationProcessing*.appendNode('profile', [default: true, name: 'Default', enabled: true])*.appendNode('processorPath', [useClasspath: true])
      }
    }
    runConfigurations {
      runMyApp {
        type = "Application"
        name = "Server"
        mainClass = "net.physalis.akita.Application"
        vmOptions = "-Dapp.env=dev"
        module = project(":")
      }
    }
  }
}

task start(dependsOn: "compileJava", type: JavaExec) {
  main = "net.physalis.akita.Application"
  classpath = sourceSets.main.runtimeClasspath
  systemProperties = [
      "log.highlight": true,
      "app.env": System.properties["app.env"]
  ]
}
